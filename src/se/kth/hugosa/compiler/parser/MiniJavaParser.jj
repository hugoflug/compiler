options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC = false;
}

PARSER_BEGIN(MiniJavaParser)
  package se.kth.hugosa.compiler.parser;
  import se.kth.hugosa.compiler.ast.*;
  public class MiniJavaParser {}
PARSER_END(MiniJavaParser)

TOKEN : {
     < CLASS: "class" >
  |  < PUBLIC : "public" >
  |  < STATIC : "static" >
  |  < VOID : "void" >
  |  < STRING : "String" >
  |  < RETURN : "return" >
  |  < INT : "int" >
  |  < LBRACKET : "[" >
  |  < RBRACKET : "]" >
  |  < BOOLEAN : "boolean" >
  |  < LBRACE : "{" >
  |  < RBRACE : "}" >
  |  < LPAREN : "(" >
  |  < RPAREN : ")" >
  |  < IF : "if" >
  |  < ELSE : "else" >
  |  < WHILE : "while" >
  |  < SYSO : "System.out.println" >
  |  < DOT : "." >
  |  < LENGTH : "length" >
  |  < TRUE : "true" >
  |  < FALSE : "false" >
  |  < THIS : "this" >
  |  < NEW : "new" >
  |  < EXCLMARK : "!" >
  |  < AND : "&&" >
  |  < OR : "||" >
  |  < LT : "<" >
  |  < LET : "<=" >
  |  < MT : ">" >
  |  < MET : ">=" >
  |  < EQ : "==" >
  |  < NOTEQ : "!=" >
  |  < PLUS : "+" >
  |  < MINUS : "-" >
  |  < MULT : "*" >
  |  < COMMA : "," >
  |  < ASSIGN : "=" >
  |  < SEMICOLON : ";" >
  |  < ID : ((["a"-"z"]|["A"-"Z"]|"_")(["a"-"z"]|["A"-"Z"]|["0"-"9"]|"_")*) >
  |  < INT_LIT : ("0"|(["1"-"9"](["0"-"9"])*)) >
}

SKIP : {
     " "
  |  "\t"
  |  "\n"
  |  "\r"
  |  "\f"
  |  <"//" (~["\r", "\n"])*>
  |  "/*" : COMMENT_STATE
}

<COMMENT_STATE> SKIP : {
      "*/" : DEFAULT
    | < ~[] >
}

Program parse() :
{
    Program program;
}
{
    program = Program()
    { return program; }
}

Program Program() :
{
    MainClass mainClass;
    ClassDeclList classDecls = new ClassDeclList();
    ClassDecl classDecl;
}
{
    mainClass = MainClass() (classDecl = ClassDecl() { classDecls.addClassDecl(classDecl); })* <EOF>
    { return new Program(mainClass, classDecls, mainClass.getLine(), mainClass.getColumn()); }
}

MainClass MainClass() :
{
    Token className;
    Token stdArgsName;
    Token main;
    Token pos;
    VarDeclList varDecls = new VarDeclList();
    VarDecl varDecl;
    StmtList statements = new StmtList();
    Stmt stmt;
}
{
    pos = <CLASS> className = <ID> <LBRACE> <PUBLIC> <STATIC> <VOID> main = <ID> <LPAREN> <STRING> <LBRACKET> <RBRACKET> stdArgsName = <ID> <RPAREN> <LBRACE>
       (LOOKAHEAD(2) varDecl = VarDecl() { varDecls.addVarDecl(varDecl); })*
       (stmt = Stmt() { statements.addStmt(stmt); })*
    <RBRACE>
    <RBRACE>

    {
        if (!main.image.equals("main")) {
            throw new ParseException();
        }
        return new MainClass(new Identifier(className.image, className.beginLine, className.beginColumn), new Identifier(stdArgsName.image, stdArgsName.beginLine, stdArgsName.beginColumn),
            varDecls, statements, pos.beginLine, pos.beginColumn);
    }
}

ClassDecl ClassDecl() :
{
    Token id;
    Token pos;
    VarDeclList varDecls = new VarDeclList();
    VarDecl varDecl;
    MethodDeclList methodDecls = new MethodDeclList();
    MethodDecl methodDecl;
}
{
    pos = <CLASS> id = <ID>
    <LBRACE> (varDecl = VarDecl() { varDecls.addVarDecl(varDecl); })*
    (methodDecl = MethodDecl() { methodDecls.addMethodDecl(methodDecl); })* <RBRACE>

    { return new ClassDecl(new Identifier(id.image, id.beginLine, id.beginColumn), varDecls, methodDecls, pos.beginLine, pos.beginColumn); }
}

Exp Exp() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpAnd() (pos = <OR> exp2 = ExpAnd() { exp1 = new Or(exp1, exp2, pos.beginLine, pos.beginColumn); })*
    { return exp1; }
}

Exp ExpAnd() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpEquality() (pos = <AND> exp2 = ExpEquality() { exp1 = new And(exp1, exp2, pos.beginLine, pos.beginColumn); })*
    { return exp1; }
}

Exp ExpEquality() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpCompare() (
          pos = <EQ> exp2 = ExpCompare() { exp1 = new Equal(exp1, exp2, pos.beginLine, pos.beginColumn); }
        | pos = <NOTEQ> exp2 = ExpCompare() { exp1 = new NotEqual(exp1, exp2, pos.beginLine, pos.beginColumn); }
    )*
    { return exp1; }
}

Exp ExpCompare() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpPlusMinus() (
          pos = <LT> exp2 = ExpPlusMinus() { exp1 = new LessThan(exp1, exp2, pos.beginLine, pos.beginColumn); }
        | pos = <MT> exp2 = ExpPlusMinus()  { exp1 = new MoreThan(exp1, exp2, pos.beginLine, pos.beginColumn); }
        | pos = <LET> exp2 = ExpPlusMinus()  { exp1 = new LessOrEqualThan(exp1, exp2, pos.beginLine, pos.beginColumn); }
        | pos = <MET> exp2 = ExpPlusMinus()  { exp1 = new MoreOrEqualThan(exp1, exp2, pos.beginLine, pos.beginColumn); }
    )*
    { return exp1; }
}

Exp ExpPlusMinus() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpMult() (
                 pos = <PLUS> exp2 = ExpMult() { exp1 = new Plus(exp1, exp2, pos.beginLine, pos.beginColumn); }
               | pos = <MINUS> exp2 = ExpMult() { exp1 = new Minus(exp1, exp2, pos.beginLine, pos.beginColumn); }
               )*
    { return exp1; }
}

Exp ExpMult() :
{
    Exp exp1;
    Exp exp2;
    Token pos;
}
{
    exp1 = ExpNot() (pos = <MULT> exp2 = ExpNot() { exp1 = new Mult(exp1, exp2, pos.beginLine, pos.beginColumn); })*
    { return exp1; }
}

Exp ExpNot ():
{
    Exp value;
    int nots = 0;
    Token pos;
}
{
    (
        (pos = <EXCLMARK> { nots++; })+ value = ExpInfo()
        {
            for (int i = 0; i < nots; i++) {
                value = new Not(value, pos.beginLine, pos.beginColumn);
            }
        }
        | value = ExpInfo()
    )

    { return value; }
}

Exp ExpInfo() :
{
    Exp value;
    Exp exp2;
    Token id;
    ExpList expList;
    Token pos;
}
{
    value = ExpParens()
    (
             LOOKAHEAD(2)
             pos = <DOT> <LENGTH> { value = new ArrayLength(value, pos.beginLine, pos.beginColumn); }
           | pos = <DOT> id = <ID> <LPAREN> expList = ExpList() { value = new MethodCall(value, new Identifier(id.image, id.beginLine, id.beginColumn), expList, pos.beginLine, pos.beginColumn); }  <RPAREN>
           | pos = <LBRACKET> exp2 = Exp() <RBRACKET> { value = new ArrayLookup(value, exp2, pos.beginLine, pos.beginColumn); }
    )*
    { return value; }
}

ExpList ExpList() :
{
    ExpList expList = new ExpList();
    Exp exp;
}
{
    [exp = Exp() { expList.addExp(exp); }(<COMMA> exp = Exp() { expList.addExp(exp); })*]
    { return expList; }
}

Exp ExpParens() :
{
    Exp value;
}
{
    (
        <LPAREN> value = Exp() <RPAREN>
        | value = ExpVal()
    )
    { return value; }
}

Exp ExpVal() :
{
   Token token = null;
   Exp value;
   Token pos;
}
{
  (
        token = <INT_LIT> { value = new IntLit(Integer.parseInt(token.image), token.beginLine, token.beginColumn); }
      | token = <TRUE> { value = new True(token.beginLine, token.beginColumn); }
      | token = <FALSE> { value = new False(token.beginLine, token.beginColumn); }
      | token = <THIS> { value = new This(token.beginLine, token.beginColumn); }
      | token = <ID> { value = new Identifier(token.image, token.beginLine, token.beginColumn); }
      | LOOKAHEAD(2)
        pos = <NEW> <INT> <LBRACKET> value = Exp() <RBRACKET> { value = new NewArray(value, pos.beginLine, pos.beginColumn); }
      | pos = <NEW> token = <ID> <LPAREN> <RPAREN> { value = new NewObject(new Identifier(token.image, token.beginLine, token.beginColumn), pos.beginLine, pos.beginColumn); }
  )
  { return value; }
}

VarDecl VarDecl() :
{
    VarDecl varDecl;
    Token id;
    Type type;
}
{
    type = Type()
    id = <ID>
    <SEMICOLON>
    { return new VarDecl(type, new Identifier(id.image, id.beginLine, id.beginColumn), type.getLine(), type.getColumn()); }
}

MethodDecl MethodDecl() :
{
    Type type;
    Token id;
    Identifier name;
    Formal formal;
    FormalList argList;
    VarDeclList varDecls = new VarDeclList();
    VarDecl varDecl;
    Stmt stmt;
    StmtList statements = new StmtList();
    Exp returnValue;
    Token pos;
}
{
    pos = <PUBLIC>
    type = Type()
    id = <ID> { name = new Identifier(id.image, id.beginLine, id.beginColumn);}
    <LPAREN> argList = FormalList() <RPAREN> <LBRACE>
    (LOOKAHEAD(2) varDecl = VarDecl() {varDecls.addVarDecl(varDecl);})*
    (stmt = Stmt() {statements.addStmt(stmt);})*
    <RETURN> returnValue = Exp() <SEMICOLON>
    <RBRACE>
    { return new MethodDecl(type, name, argList, varDecls, statements, returnValue, pos.beginLine, pos.beginColumn); }
}

FormalList FormalList() :
{
    FormalList formalList = new FormalList();
    Type type;
    Token id;
    Identifier name;
}
{
    [type = Type() id = <ID> { formalList.addFormal(new Formal(type, new Identifier(id.image, id.beginLine, id.beginColumn), type.getLine(), type.getColumn())); }
    (<COMMA> type = Type() id = <ID> { formalList.addFormal(new Formal(type, new Identifier(id.image, id.beginLine, id.beginColumn), type.getLine(), type.getColumn())); } )*]

    { return formalList; }
}

Type Type() :
{
    Type type;
    Token id;
    Token pos;
}
{
    (
          LOOKAHEAD(2)
          pos = <INT> <LBRACKET> <RBRACKET> { type = new IntArrayType(pos.beginLine, pos.beginColumn); }
        | pos = <BOOLEAN> { type = new BooleanType(pos.beginLine, pos.beginColumn); }
        | pos = <INT> { type = new IntType(pos.beginLine, pos.beginColumn); }
        | id = <ID> { type = new ObjectType(id.image, id.beginLine, id.beginColumn); }
    )
    { return type; }
}

Stmt ElseStmt(Exp condition, Stmt thenStmt, Token pos) :
{
    Stmt stmt;
    Stmt elseStmt;
}
{
    ( <ELSE> elseStmt = Stmt() { stmt = new If(condition, thenStmt, elseStmt, pos.beginLine, pos.beginColumn); }
     |{} { stmt = new IfWithoutElse(condition, thenStmt, pos.beginLine, pos.beginColumn); }
    )
    { return stmt; }
}

Stmt Stmt() :
{
    Stmt stmt;
    Stmt stmt2;
    StmtList stmtList = new StmtList();
    Exp exp1;
    Exp exp2;
    Token id;
    Token pos;
}
{
    (
            pos = <LBRACE> (stmt = Stmt() { stmtList.addStmt(stmt); })* <RBRACE> { stmt = new Block(stmtList, pos.beginLine, pos.beginColumn); }
          | pos = <IF> <LPAREN> exp1 = Exp() <RPAREN> stmt = Stmt() stmt2 = ElseStmt(exp1, stmt, pos) { stmt = stmt2; }
          | pos = <WHILE> <LPAREN> exp1 = Exp() <RPAREN> stmt = Stmt() { stmt = new While(exp1, stmt, pos.beginLine, pos.beginColumn); }
          | pos = <SYSO> <LPAREN> exp1 = Exp() <RPAREN> <SEMICOLON> { stmt = new Syso(exp1, pos.beginLine, pos.beginColumn); }
          | LOOKAHEAD(2)
            id = <ID> <ASSIGN> exp1 = Exp() <SEMICOLON> { stmt = new Assign(new Identifier(id.image, id.beginLine, id.beginColumn), exp1, id.beginLine, id.beginColumn); }
          | id = <ID> <LBRACKET> exp1 = Exp() <RBRACKET> <ASSIGN> exp2 = Exp() <SEMICOLON> { stmt = new ArrayAssign(new Identifier(id.image, id.beginLine, id.beginColumn), exp1, exp2, id.beginLine, id.beginColumn ); }
    )
  { return stmt; }
}